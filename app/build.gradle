apply plugin: 'com.android.application'

android {
    buildToolsVersion '28.0.3'
    compileSdkVersion 28 // 最高运行版本
    defaultConfig {
        applicationId "com.jiangzg.lovenote" // 要和manifest一致
        targetSdkVersion 28 // 最高效果版本
        minSdkVersion 21 // 最低运行版本
        multiDexEnabled true // 允许分包
        versionCode 20
        versionName "1.2.0"
        vectorDrawables.useSupportLibrary = true // 用于支持vectorDrawables
        flavorDimensions "release" // 多渠道打包分支
        ndk {
            // ali_push/fresco 选择要添加的对应cpu类型的.so库。 , "mips", "mips64"
            abiFilters 'armeabi', "armeabi-v7a", "arm64-v8a", 'x86', "x86_64"
        }
    }
    signingConfigs { // 签名信息
        jzg_01 {
            storeFile file('./key/jzg_01')
            storePassword 'laojiang1'
            keyAlias 'jzg_01'
            keyPassword 'laojiang1'
            // MD5 = 76:52:5C:1C:8E:17:3B:45:B0:BF:17:01:C9:AD:24:5C
            // MD5 = 76525c1c8e173b45b0bf1701c9ad245c
            // SHA1 = B5:1B:D6:6B:B5:C0:7A:8A:D3:EC:E1:6B:08:A0:F4:7C:C3:0C:27:DA
            // SHA256 = 9C:65:BE:24:EC:6A:4D:9B:9A:5B:A0:32:0A:87:2E:67:3C:73:17:AA:E9:73:A4:FE:AD:2D:DF:56:48:A5:57:25
        }
    }
    buildTypes { // 打包选项
        debug { // 测试
            signingConfig signingConfigs.jzg_01 // 设置签名信息
            minifyEnabled false // 混淆开关(测试最好关掉，否则会影响断点调试)
            zipAlignEnabled true // 内存对齐
            shrinkResources false // 移除无用的resource文件
            // 混淆规则文件
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release { // 正式
            signingConfig signingConfigs.jzg_01 // 可以只用一个签名信息
            minifyEnabled true
            zipAlignEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        applicationVariants.all { variant ->
            variant.outputs.all { output ->
                def fileName
                if (variant.buildType.name == 'release') {
                    // 输出apk名称为 软件名_版本号_渠道_日期.apk
                    def date = new Date().format("yyyy-MM-dd_HH-mm", TimeZone.default)
                    fileName = "${android.defaultConfig.versionCode}_${variant.productFlavors[0].name}_${date}"
                } else {
                    fileName = "debug"
                }
                outputFileName = "ys_" + fileName + ".apk"
            }
        }
    }
    productFlavors { // 发布渠道
        official {} // 官方
        //---------------------手机厂商----------------------
//        google {} // 谷歌
        huawei {} // 华为
        oppo {} // oppo
        vivo {} // vivo
        xiaomi {} // 小米
        meizu {} // 魅族
        samsung {} // 三星
        //---------------------三方市场----------------------
        tencent {} // 应用宝
        ali {} // 阿里
        baidu {} // 百度
        qh360 {} // 360
        kuan {} // 酷安
    }
    productFlavors.all {
        flavor -> flavor.manifestPlaceholders = [MARKET_CHANNEL_VALUE: name]
    }
    sourceSets { // 源码设置
        main {
            java.srcDirs = ['src/main/java', 'src/main/aidl']
            jniLibs.srcDirs = ['libs'] // 指定lib库目录
        }
        androidTest.setRoot('tests') // 指定测试所在的路径
        debug.setRoot('build-types/debug') // 指定debug模式的路径
        release.setRoot('build-types/release') // 指定release模式的路径
    }
    compileOptions { // 编码/JDK等
        encoding "UTF-8"
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lintOptions { // 编译的lint检查
        disable 'MissingTranslation' // 防止在发布的时候出现因MissingTranslation导致Build Failed!
        checkReleaseBuilds false // 检查release资源
        abortOnError false // 移除lint检查的error
    }
    packagingOptions { // 解决jar包冲突
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/dependencies.txt'
        exclude 'META-INFTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/NOTICE.txt'
    }
    dexOptions { // 构建配置(加快编译速度)
        preDexLibraries true // 预先编译依赖库
        maxProcessCount 8 // 最大线程数
        javaMaxHeapSize "4g" // 最大堆大小
    }
    aaptOptions { // 过滤PNG图片检验
        cruncherEnabled = false
        useNewCruncher = false
    }
}

configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        def requested = details.requested
        if (requested.group == 'com.android.support') {
            if (!requested.name.startsWith("multidex")) {
                // 统一所有support版本号
                details.useVersion '28.0.0'
            }
        }
    }
}

dependencies { // 及时更新
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation project(':GBase')
    implementation 'com.android.support:multidex:1.0.3'
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support:support-v4:28.0.0'
    implementation 'com.android.support:design:28.0.0'
    implementation 'com.android.support:recyclerview-v7:28.0.0'
    implementation 'com.android.support:cardview-v7:28.0.0'
    implementation 'com.android.support:palette-v7:28.0.0'
    implementation 'com.android.support:support-vector-drawable:28.0.0'
    implementation 'com.jakewharton:butterknife:9.0.0' // 适配28 -> up
    annotationProcessor 'com.jakewharton:butterknife-compiler:9.0.0' // 适配28 -> up
    implementation 'io.reactivex:rxjava:1.3.0' // 方法属性修改 -> up
    implementation 'io.reactivex:rxandroid:1.2.1' // 方法属性修改 -> up
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation 'com.squareup.retrofit2:retrofit:2.5.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava:2.5.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.5.0'
    implementation 'com.squareup.retrofit2:converter-scalars:2.5.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.14.1'
    // UI
    implementation 'top.zibin:Luban:1.1.8'
    implementation 'me.relex:photodraweeview:1.1.3' // 适配28 -> up
    implementation 'com.facebook.fresco:fresco:1.13.0'
    implementation 'com.facebook.fresco:webpsupport:1.13.0'
    implementation 'com.facebook.fresco:animated-gif:1.13.0'
    implementation 'com.facebook.fresco:animated-webp:1.13.0'
    implementation 'com.facebook.fresco:imagepipeline-okhttp3:1.13.0'
    implementation 'com.haibin:calendarview:3.5.9'
    implementation 'com.youth.banner:banner:1.4.10'
    implementation 'com.zhihu.android:matisse:0.5.2-beta4'
    implementation 'com.github.bumptech.glide:glide:3.7.0' // 搭配matisse -> up
    implementation 'com.afollestad.material-dialogs:core:0.9.6.0' // 适配28 -> up
    implementation 'com.github.CymChad:BaseRecyclerViewAdapterHelper:2.9.14' // wea/pic修正 -> up
    implementation 'com.google.android.exoplayer:exoplayer:2.9.6'
    // 三方
    implementation files('libs/alipaySdk-20180601.jar') // AliPay
    implementation 'com.tencent.mm.opensdk:wechat-sdk-android-with-mta:5.3.1' // WXPay
    implementation 'com.aliyun.dpa:oss-android-sdk:2.9.2' // OSS
    implementation 'com.aliyun.ams:alicloud-android-push:3.1.4' // AliPush
    implementation 'com.aliyun.ams:alicloud-android-third-push:3.0.6@aar' // AliPush(辅助通道)
    implementation 'com.amap.api:location:4.5.1' // 地图定位
    implementation 'com.amap.api:map2d:5.2.0' // 地图2D
    implementation 'com.amap.api:search:6.5.0.1' // 地图搜索
}
